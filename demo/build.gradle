
description = '<h3>Jtest demo project</h3><p>The project contains sample source code with various errors, and bad practice examples which can be detected by Jtest analysis.</p>'

apply plugin: 'java'
apply plugin: 'maven'

group 'com.parasoft'

apply from: projectDir.absolutePath + '/../../integration/gradle/jtest.gradle'

sourceCompatibility = JavaVersion.VERSION_1_5
targetCompatibility = JavaVersion.VERSION_1_5

compileJava.options.encoding = 'UTF-8'

test{
    // ignore test failures for demo purposes
    ignoreFailures = true

    // turn off scan for test classes, use include/exclude pattern only
    // AccountDynamicTest.class is a part of AccountDynamicSuite.class and should not be launched as a separated test
    scanForTestClasses = false
    include '**/Test*.class'
    include '**/*Test.class'
    include '**/*TestCase.class'
    include '**/*Suite.class'
    exclude '**/nbank/AccountDynamicTest.class'
}

buildscript {
    repositories {
        mavenCentral()
    }
}

jtest {
    // enforcing choosen configuration
    // config = "builtin://Demo Configuration"
    settings = "demo.properties"
    exclude = "path:**/tests/**"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "./demo-repository"
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['src']
            include '**/*.gif'
        }
    }
    test {
        java {
            srcDirs = ['tests']
        }
        resources.srcDirs = []
    }
}

dependencies {
    compile 'javax.servlet:servlet-api:2.4'
    compile 'org.powermock:powermock-module-junit4:1.5.5'
    compile 'org.powermock:powermock-api-mockito:1.5.5'
    testCompile 'junit:junit:4.11'
}
